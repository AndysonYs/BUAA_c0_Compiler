# BUAA_c0_Compiler
北航计算机学院编译原理课程设计大作业  
基于Ubuntu18.04开发,c++11,使用工具clion. 


## 文法
> ＜加法运算符＞ ::= +｜-  
>＜乘法运算符＞  ::= *｜/  
>＜关系运算符＞  ::=  <｜<=｜>｜>=｜!=｜==  
>＜字母＞   ::= ＿｜a｜．．．｜z｜A｜．．．｜Z  
>＜数字＞   ::= ０｜＜非零数字＞  
>＜非零数字＞  ::= １｜．．．｜９  
>＜字符＞    ::=  '＜加法运算符＞'｜'＜乘法运算符＞'｜'＜字母＞'｜'＜数字＞'  
>＜字符串＞   ::=  "｛十进制编码为32,33,35-126的ASCII字符｝"  
>＜程序＞    ::= ［＜常量说明＞］［＜变量说明＞］{＜有返回值函数定义＞|＜无返回值函数定义＞}＜主函数＞  
>＜常量说明＞ ::=  const＜常量定义＞;{ const＜常量定义＞;}  
>＜常量定义＞   ::=   int＜标识符＞＝＜整数＞{,＜标识符＞＝＜整数＞}  
                               | char＜标识符＞＝＜字符＞{,＜标识符＞＝＜字符＞}                              
>＜无符号整数＞  ::= ＜非零数字＞｛＜数字＞｝| 0  
>＜整数＞        ::= ［＋｜－］＜无符号整数＞  
>＜标识符＞    ::=  ＜字母＞｛＜字母＞｜＜数字＞｝  
>＜声明头部＞   ::=  int＜标识符＞ |char＜标识符＞  
>＜变量说明＞  ::= ＜变量定义＞;{＜变量定义＞;}  
>＜变量定义＞  ::= ＜类型标识符＞(＜标识符＞|＜标识符＞'\['＜无符号整数＞'\]'){,(＜标识符＞|＜标识符＞'\['＜无符号整数＞'\]' )} //＜无符号整数＞表示数组元素的个数，其值需大于0  
>＜常量＞   ::=  ＜整数＞|＜字符＞  
>＜类型标识符＞      ::=  int | char  
>＜有返回值函数定义＞  ::=  ＜声明头部＞'('＜参数表＞')' '{'＜复合语句＞'}'  
>＜无返回值函数定义＞  ::= void＜标识符＞'('＜参数表＞')''{'＜复合语句＞'}'  
>＜复合语句＞   ::=  ［＜常量说明＞］［＜变量说明＞］＜语句列＞  
>＜参数表＞    ::= ＜参数＞{,＜参数＞}| ＜空>  
>＜参数＞    ::=  ＜类型标识符＞＜标识符＞  
>＜主函数＞    ::= void main'('')''{'＜复合语句＞'}'  
>＜表达式＞    ::= ［＋｜－］＜项＞{＜加法运算符＞＜项＞}   //\[+|-\]只作用于第一个<项>  
>＜项＞     ::= ＜因子＞{＜乘法运算符＞＜因子＞}  
>＜因子＞    ::= ＜标识符＞｜＜标识符＞'\['＜表达式＞'\]'|'('＜表达式＞')'｜＜整数＞|＜字符＞｜＜有返回值函数调用语句＞           
>＜语句＞    ::= ＜条件语句＞｜＜循环语句＞| '{'＜语句列＞'}'| ＜有返回值函数调用语句＞;   
                           |＜无返回值函数调用语句＞;｜＜赋值语句＞;｜＜读语句＞;｜＜写语句＞;｜＜空＞;|＜情况语句＞｜＜返回语句＞;  
>＜赋值语句＞   ::=  ＜标识符＞＝＜表达式＞|＜标识符＞'\['＜表达式＞'\]'=＜表达式＞  
>＜条件语句＞  ::=  if '('＜条件＞')'＜语句＞  
>＜条件＞    ::=  ＜表达式＞＜关系运算符＞＜表达式＞｜＜表达式＞ //表达式为0条件为假，否则为真  
>＜循环语句＞   ::=  while '('＜条件＞')'＜语句＞  
>＜情况语句＞  ::=  switch '('＜表达式＞')' '{'＜情况表＞＜缺省＞ '}'  
>＜情况表＞   ::=  ＜情况子语句＞{＜情况子语句＞}  
>＜情况子语句＞  ::=  case＜常量＞：＜语句＞  
>＜缺省＞   ::=  default : ＜语句＞|＜空＞  
>＜有返回值函数调用语句＞ ::= ＜标识符＞'('＜值参数表＞')'  
>＜无返回值函数调用语句＞ ::= ＜标识符＞'('＜值参数表＞')'  
>＜值参数表＞   ::= ＜表达式＞{,＜表达式＞}｜＜空＞  
>＜语句列＞   ::= ｛＜语句＞｝  
>＜读语句＞    ::=  scanf '('＜标识符＞{,＜标识符＞}')'  
>＜写语句＞    ::= printf '(' ＜字符串＞,＜表达式＞ ')'| printf '('＜字符串＞ ')'| printf '('＜表达式＞')'  
>＜返回语句＞   ::=  return\['('＜表达式＞')'\]     


附加说明：

（1）char类型的变量或常量，用字符的ASCII码对应的整数参加运算

（2）标识符区分大小写字母

（3）写语句中，字符串原样输出，单个char类型的变量或常量输出字符，其他表达式按整型输出

（4）情况语句中，switch后面的表达式和case后面的常量只允许出现int和char类型；每个情况子语句执行完毕后，不继续执行后面的情况子语句

（5）数组的下标从0开始 

## 限制
支持最大字符串长度 1000  
最大识别符数 10000  
最大单个识别符长度 1000  
最大MIPS.text代码长度 6000  
最大MIPS.data代码长度 6000  

## 优化
常数合并与传播
全局寄存器分配（引用计数）  
窥孔优化   
待完成：局部公共子表达式消除


## 寄存器使用约定
$0 保持常0  
$1 保留给MARS使用  
$a0 用作函数传参  
$v0 用作syscall参数或函数返回值  
$s0 用作函数运行栈基地址  
$s1 用作保存四元式第一个操作数或四元式运算结果  
$s2 用作保存四元式第二个操作数  
$s4 用作表达式临时变量  
$s4 switch变量  
$t0, $t1 用作临时操作变量, $t1仅在数组赋值时保存数组基地址  
$t2 用作窥孔优化  
$sp 用作栈指针  
$ra 用作返回地址  

### 可使用的11个全局寄存器
**$11, $12, $13, $14, $15, $19, $21, $22, $23, $24, $25($t3-$t9, $s3, $s5-$s7)**